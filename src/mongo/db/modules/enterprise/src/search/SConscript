# -*- mode: python -*-

Import("env")

env = env.Clone()

env.SConscript(dirs=['mongotmock'], exports=['env'])

env.Library(
    target='document_source_search',
    source=[
        'document_source_search.cpp',
        'document_source_search_meta.cpp',
        'document_source_internal_search_id_lookup.cpp',
        'document_source_internal_search_mongot_remote.cpp',
        'document_source_internal_search_mongot_remote.idl',
        'mongot_cursor.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/db/pipeline/pipeline',
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/mongo/db/pipeline/sharded_agg_helpers',
        '$BUILD_DIR/mongo/db/s/sharding_api_d',
        '$BUILD_DIR/mongo/db/server_base',
        '$BUILD_DIR/mongo/db/shard_filterer',
        '$BUILD_DIR/mongo/executor/task_executor_cursor',
        'mongot_options',
        'mongot_task_executor',
    ],
    LIBDEPS_DEPENDENTS=[
        '$BUILD_DIR/mongo/db/mongod_initializers',
        '$BUILD_DIR/mongo/s/mongos_initializers',
    ],
    LIBDEPS_TAGS=[
        'lint-allow-nonprivate-on-deps-dependents',
        'lint-allow-bidirectional-edges',
    ],
)

env.Library(
    target='mongot_options',
    source=[
        'mongot_options.cpp',
        'mongot_options.idl',
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/mongo/db/server_base',
        '$BUILD_DIR/mongo/util/net/network',
    ],
)

env.Library(
    target='mongot_task_executor',
    source=[
        'mongot_task_executor.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/executor/network_interface_factory',
        '$BUILD_DIR/mongo/executor/network_interface_thread_pool',
        '$BUILD_DIR/mongo/executor/task_executor_interface',
        '$BUILD_DIR/mongo/executor/thread_pool_task_executor',
        '$BUILD_DIR/mongo/util/concurrency/thread_pool',
    ],
)

env.Library(
    target='search_cqf_utils',
    source=[
        'cqf_utils.cpp',
    ],
    LIBDEPS_PRIVATE=[
        'document_source_search',
    ],
    LIBDEPS_DEPENDENTS=[
        '$BUILD_DIR/mongo/db/mongod_initializers',
        '$BUILD_DIR/mongo/s/mongos_initializers',
    ],
)

env.CppIntegrationTest(
    target='mongot_task_executor_integration_test',
    source=[
        'mongot_task_executor_integration_test.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/db/wire_version',
        '$BUILD_DIR/mongo/executor/network_interface',
        '$BUILD_DIR/mongo/transport/transport_layer_egress_init',
        '$BUILD_DIR/mongo/util/version_impl',
        'mongot_task_executor',
    ],
)

env.CppUnitTest(
    target='enterprise_search_test',
    source=[
        'document_source_search_test.cpp',
        'document_source_search_meta_test.cpp',
        'document_source_internal_search_id_lookup_test.cpp',
        'document_source_internal_search_mongot_remote_test.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/db/auth/authmocks',
        '$BUILD_DIR/mongo/db/exec/document_value/document_value_test_util',
        '$BUILD_DIR/mongo/db/pipeline/document_source_mock',
        '$BUILD_DIR/mongo/db/service_context_test_fixture',
        'document_source_search',
        'mongot_options',
    ],
)
