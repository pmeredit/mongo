# -*- mode: python -*-

Import('env')

env = env.Clone()

env.Library(
    target='ldap_manager_init',
    source=[
        'ldap_manager_init.cpp'
    ],
    LIBDEPS=['$BUILD_DIR/mongo/db/service_context',
             'ldap_manager'],
    LIBDEPS_DEPENDENTS=['$BUILD_DIR/mongo/mongodmain'],
    PROGDEPS_DEPENDENTS=['$BUILD_DIR/mongo/mongos']
)

env.Library(
    target='ldap_manager',
    source=[
        'ldap_manager.cpp',
        'ldap_manager_impl.cpp',
        'ldap_options.cpp',
        'ldap_runtime_parameters.cpp',
        'ldap_runner_impl.cpp'
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/db/auth/authcore',
        'ldap_connection',
        'ldap_name_map',
        'ldap_query',
    ],
)

env.Library(
    target='ldap_options_mongod',
    source=[
        'ldap_options_mongod.cpp',
    ],
    LIBDEPS=[
        'ldap_manager',
    ],
    LIBDEPS_DEPENDENTS=[
        '$BUILD_DIR/mongo/mongodmain',
    ],
)

env.Library(
    target='ldap_options_shared',
    source=[
        'ldap_options_shared.cpp',
    ],
    LIBDEPS=[
        'ldap_manager',
    ],
    PROGDEPS_DEPENDENTS=[
        '$BUILD_DIR/mongo/mongos',
    ],
)

env.Library(
    target='ldap_query',
    source=[
        'ldap_connection_options.cpp',
        'ldap_query.cpp',
        'ldap_query_config.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/mongo/base',
        '$BUILD_DIR/mongo/base/secure_allocator',
        '$BUILD_DIR/mongo/util/net/network',
        '$BUILD_DIR/mongo/util/options_parser/options_parser',
        '$BUILD_DIR/third_party/shim_pcrecpp',
    ],
)

env.CppUnitTest(
    target='ldap_query_test',
    source=[
        'ldap_query_test.cpp',
    ],
    LIBDEPS=[
        'ldap_query',
    ],
)

env.CppUnitTest(
    target='ldap_connection_options_test',
    source=[
        'ldap_connection_options_test.cpp',
    ],
    LIBDEPS=[
        'ldap_query',
    ],
)

ldap_connection_sources = [
    'connections/ldap_connection_factory.cpp',
    'connections/ldap_connection_helpers.cpp',
]

if env.TargetOSIs('windows'):
    ldap_connection_sources.append('connections/windows_ldap_connection.cpp')
else:
    ldap_connection_sources.append('connections/openldap_connection.cpp')

env.Library(
    target='ldap_connection',
    source=ldap_connection_sources,
    LIBDEPS=[
        'ldap_query',
        '$BUILD_DIR/mongo/base',
        '$BUILD_DIR/mongo/util/net/network',
    ],
    SYSLIBDEPS=env.get("MONGO_LDAP_LIB", [])
)

env.Library(
    target='mongo_ldap',
    source=[
        'authz_manager_external_state_ldap.cpp',
        'ldap_user_cache_invalidator_job.cpp',
    ],
    LIBDEPS=[
        'ldap_manager',
        'ldap_query',
        'ldap_name_map',
        '$BUILD_DIR/mongo/bson/mutable/mutable_bson',
        '$BUILD_DIR/mongo/db/auth/authcore',
        '$BUILD_DIR/mongo/db/auth/authmongod',
        '$BUILD_DIR/mongo/db/mongodandmongos',
        '$BUILD_DIR/mongo/util/background_job',
        '$BUILD_DIR/mongo/util/options_parser/options_parser',
    ],
    LIBDEPS_DEPENDENTS=[
        '$BUILD_DIR/mongo/mongodmain',
    ],
)

env.Library(
    target='ldap_name_map',
    source=[
        'name_mapping/internal_to_ldap_user_name_mapper.cpp',
        'name_mapping/rewrite_rule.cpp',
        'name_mapping/ldap_rewrite_rule.cpp',
        'name_mapping/regex_rewrite_rule.cpp',
    ],
    LIBDEPS=[
        'ldap_query',
        '$BUILD_DIR/mongo/base',
        '$BUILD_DIR/mongo/db/service_context',
        '$BUILD_DIR/third_party/shim_pcrecpp',
    ],
)

env.CppUnitTest(
    target='rewrite_rule_test',
    source=[
        'name_mapping/rewrite_rule_test.cpp',
    ],
    LIBDEPS=[
        'ldap_manager',
        'ldap_name_map',
        '$BUILD_DIR/mongo/db/auth/authcore',
        '$BUILD_DIR/mongo/db/service_context',
    ],
)
