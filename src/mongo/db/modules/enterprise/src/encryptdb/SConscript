# -*- mode: python -*-
Import("env")
Import("wiredtiger")

if not wiredtiger:
    env.FatalError("EncryptedStorage requires WiredTiger")

wtEnv = env.Clone()
wtEnv.InjectThirdPartyIncludePaths(libraries=['wiredtiger'])
wtEnv.InjectThirdPartyIncludePaths(libraries=['wiredtiger_ext'])
# Ubuntu1204 exposes GCM symbols, but they do not work. We have to detect the platform and disable
# that portion of the symmetric_crypto test.
if wtEnv['MONGO_DISTMOD'] == "ubuntu1204":
    wtEnv.Append(CPPDEFINES=["DISABLE_GCM_TESTVECTORS"]);


wtEnv.Library(target='encryption_key_manager_init',
            source=[
                'encryption_key_manager_init.cpp',
            ],
            LIBDEPS=[
                'encryptdb',
                'symmetric_crypto'
            ],
            PROGDEPS_DEPENDENTS=[
                '$BUILD_DIR/mongo/mongod'
            ],
            LIBDEPS_TAGS=[
                # Depends on storageGlobalParams which is in serverOnlyFiles
                'incomplete'
            ])

wtEnv.Library(target='encryptdb',
              source=[
                   'encryption_key_manager.cpp',
                   'kmip_service.cpp',
                   'wiredtiger_encryption_callbacks.cpp',
              ],
              LIBDEPS=[
                   'encryption_options',
                   'kmip',
                   'symmetric_crypto',
                   '$BUILD_DIR/mongo/base',
                   '$BUILD_DIR/mongo/db/auth/security_file',
                   '$BUILD_DIR/mongo/db/storage/wiredtiger/storage_wiredtiger_core',
                   '$BUILD_DIR/third_party/shim_wiredtiger',
              ])

env.Library(target='encryption_options',
            source=[
                'encryption_options.cpp',
                'encryption_options_init.cpp',
            ],
            LIBDEPS=[
                '$BUILD_DIR/mongo/util/options_parser/options_parser',
            ])

env.Library(target='symmetric_crypto',
            source=[
                'symmetric_crypto.cpp',
                'symmetric_crypto_smoke.cpp',
                'symmetric_key.cpp',
            ],
            LIBDEPS=[
                '$BUILD_DIR/mongo/util/net/network',
                '$BUILD_DIR/mongo/util/secure_zero_memory',
            ])

env.Library(target='kmip',
            source=[
                'kmip_request.cpp',
                'kmip_response.cpp',
            ],
            LIBDEPS=[
                '$BUILD_DIR/mongo/base',
                'symmetric_crypto',
            ])

wtEnv.CppUnitTest(target='symmetric_crypto_test',
                source='symmetric_crypto_test.cpp',
                LIBDEPS=[
                    'symmetric_crypto',
                    '$BUILD_DIR/mongo/util/net/network',
                ])

env.CppUnitTest(target='kmip_response_test',
                source='kmip_response_test.cpp',
                LIBDEPS=[
                    'kmip',
                    'symmetric_crypto',
                    '$BUILD_DIR/mongo/util/net/network',
                ])

env.CppUnitTest(target='kmip_request_test',
                source='kmip_request_test.cpp',
                LIBDEPS=[
                    'kmip',
                    'symmetric_crypto',
                    '$BUILD_DIR/mongo/util/net/network',
                ])

wtEnv.CppUnitTest(target='wiredtiger_encryption_test',
                source='wiredtiger_encryption_test.cpp',
                LIBDEPS=[
                    '$BUILD_DIR/mongo/db/auth/security_file',
                    '$BUILD_DIR/mongo/db/service_context',
                    '$BUILD_DIR/mongo/db/storage/wiredtiger/storage_wiredtiger_mock',
                    '$BUILD_DIR/mongo/util/net/network',
                    'encryptdb',
                    'symmetric_crypto',
                ])
